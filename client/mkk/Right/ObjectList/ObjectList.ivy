expr {=
from fir.controls.utils import cls, firScopeOptsEnc;
def renderObject {=
	def.kv
		objKey: null
		idxByKey: {}
		objectList: null
		level: 0;
	def.body noscope {*
		{=
			var item: {=at idxByKey objKey};
			var obj: {=at objectList item.idx};
		}
		<div class="{=cls 'rightObject'}" style="margin-left: {{level*16}}px;">
			<div>Номер: {{obj.num}}</div>
			<div>Имя: {{obj.name}}</div>
			<div>Описание: {{obj.description}}</div>
			<div>Родитель: {{obj.parentNum}}</div>
			<div>
				<a href="/dyn/right/object/right/list?num={{obj.num}}">Права</a>
			</div>
		</div>
		{=repeat childKey in item.children {=renderObject
			objKey: childKey
			idxByKey: idxByKey
			objectList: objectList
			level: level + 1
		} }
	*}
};
def ObjectList {=
	def.kv
		moduleName: 'mkk/Right/ObjectList/ObjectList'
		instanceName: 'rightObjectList'
		cssBaseClass: 'mkk-RightObjectList'
		objectList: null;
	def.body {*
	<div class="{=cls 'block'}" data-fir-module="{{moduleName}}">
		<input type="hidden" data-fir-opts="{=firScopeOptsEnc}"/>
		<h2>Объекты доступа</h2>
		{=
			var roots: [];
			var idxByKey: {};
			var i: 0;
			for obj in objectList {=
				var parentNum: {=str obj.parentNum};
				var num: {=str obj.num};
				if not obj.parentNum {=
					insert roots num null
				} else {=
					var dat: {=
						if {=has idxByKey parentNum}
							{=at idxByKey parentNum}
						else
							{idx: null, children: []}
					};
					insert dat.children num null;
					setat idxByKey dat parentNum;
				};
				var dat2: {=
					if {=has idxByKey num}
						{=at idxByKey num}
					else
						{idx: i, children: []}
				};
				set dat2.idx: i;
				setat idxByKey dat2 num;
				set i: i + 1;
			};
			repeat root in roots {=renderObject
				objKey: root
				idxByKey: idxByKey
				objectList: objectList
			};
		}
	</div>
	*}
};
}