expr {=
from fir.controls.utils import cls, firScopeOptsEnc;
def renderObject {=
	def.kv
		objKey: null
		idxByKey: {}
		objectList: null
		level: 0
		isSelect: null;
	def.body noscope {*
		{=
			var item: {=at idxByKey objKey};
			var obj: {=at objectList item.idx};
		}
		<div class="{=cls 'rightObject'}"
			style="margin-left: {{level*16}}px;"
			data-record-num="{{obj.num}}">
			<div class="{=cls 'objTitle'}">
				<a href="/dyn/right/object/edit?num={{obj.num}}"
					class="{=cls 'editLink'}"
					>
					<span>#{{obj.num}}</span>
					<span>{{obj.name}}</span>
				</a>
				<a href="/dyn/right/object/edit?parentNum={{obj.parentNum}}"
					class="{=cls 'addLink'} btn btn-primary btn-sm"
					>+</a>
			</div>
			<div>{{obj.description}}</div>
		</div>
		{=repeat childKey in item.children {=renderObject
			objKey: childKey
			idxByKey: idxByKey
			objectList: objectList
			level: level + 1
			isSelect: isSelect
		} }
	*}
};
def RightObjectList {=
	def.kv
		moduleName: 'mkk/Right/Object/List/List'
		instanceName: 'rightObjectList'
		cssBaseClass: 'mkk-RightObjectList'
		requestURI: "/api/right/object/list"
		RPCMethod: 'right.object.list'
		objectList: null
		isSelect: null;
	def.body {*
	<div class="{=cls 'block'}" data-fir-module="{{moduleName}}">
		<input type="hidden" data-fir-opts="{=firScopeOptsEnc fields: ['objectList']}"/>
		<h2>Объекты доступа</h2>
		{=
			var roots: [];
			var idxByKey: {};
			var i: 0;
			for obj in objectList {=
				var parentNum: {=str obj.parentNum};
				var num: {=str obj.num};
				if not obj.parentNum {=
					insert roots num null
				} else {=
					var dat: {=
						if {=has idxByKey parentNum}
							{=at idxByKey parentNum}
						else
							{idx: null, children: []}
					};
					insert dat.children num null;
					setat idxByKey dat parentNum;
				};
				var dat2: {=
					if {=has idxByKey num}
						{=at idxByKey num}
					else
						{idx: i, children: []}
				};
				set dat2.idx: i;
				setat idxByKey dat2 num;
				set i: i + 1;
			};
		}
		<div class="{=cls 'listView'}">
		{=
			repeat root in roots {=renderObject
				objKey: root
				idxByKey: idxByKey
				objectList: objectList
				isSelect: isSelect
			};
		}
		</div>
	</div>
	*}
};
}