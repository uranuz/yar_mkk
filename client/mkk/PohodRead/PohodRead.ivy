expr {=
from fir.controls.utils import recGet, recGetStr, cls, firScopeOptsEnc, isInRoles;
from mkk.Helpers import DateRangePrinter;
from mkk.Helpers.EntityProperty import EntityProperty;
from mkk.Tourist.NavigatedList import TouristNavigatedList;
def PohodRead {=
	def.kv
		moduleName: "mkk/PohodRead/PohodRead"
		instanceName: 'pohodRead'
		cssBaseClass: 'mkk-PohodRead'
		pohod: null
		extraFileLinks: null
		partyList: null;
	def.body {*
	<div class="{=cls 'block'}" data-fir-module="{{moduleName}}">
		<input type="hidden" data-fir-opts="{=firScopeOptsEnc fields: ['partyList']}"/>
		<h2>Данные похода</h2>
		<div class="{=cls 'topButtons'}">
			{=if {=isInRoles ['admin', 'moder']} {*
			<a href="{{vpaths.siteDynamic}}pohod/history?num={=recGet pohod 'num'}"
				class="btn btn-sm btn-secondary"
				>История</a>
			<a href="{{vpaths.siteDynamic}}pohod/edit?num={=recGet pohod 'num'}"
				class="btn btn-sm btn-secondary"
				>Редактировать</a>
			*}}
		</div>
		{%
			EntityProperty
				title: 'Код МКК'
				value: {=recGet pohod 'mkkCode'};
			EntityProperty
				title: 'Маршрутная книжка'
				value: {=recGet pohod 'bookNum'};
			EntityProperty
				title: 'Группа туристов'
				value: {%recGet pohod 'organization'; expr ' '; recGet pohod 'partyRegion'};
			EntityProperty
				title: 'Сроки похода'
				value: {=DateRangePrinter
					begin: {=recGet pohod 'beginDate'}
					end: {=recGet pohod 'finishDate'}
				};
			EntityProperty
				title: 'Вид туризма'
				value: {=recGetStr pohod 'tourismKind'};
			EntityProperty
				title: 'Категория сложности'
				value: {%recGetStr pohod 'complexity'; expr ' '; recGetStr pohod 'complexityElems'};
			EntityProperty
				title: 'Район похода'
				value: {=recGet pohod 'pohodRegion'};
			EntityProperty
				title: 'Маршрут'
				value: {=recGet pohod 'route'}
				isWide: true;
			EntityProperty
				title: 'В составе'
				value: {=recGet pohod 'partySize'};
			EntityProperty
				title: 'Руководитель группы'
				value: {=recGet pohod 'chiefName'};
			EntityProperty
				title: 'Зам. руководителя группы'
				value: {=recGet pohod 'altChiefName'};
			EntityProperty
				instanceName: 'partyListProp'
				title: 'Состав группы'
				value: {=if partyList != null
					{=
						var sliceLen: {=len partyList};
						if sliceLen > 10 {=set sliceLen: 10};
						TouristNavigatedList
							instanceName: 'partyList'
							touristList: {=slice partyList 0 sliceLen}
					}
				}
				isWide: true;
			EntityProperty
				title: 'Готовность похода'
				value: {=recGetStr pohod 'progress'};
			EntityProperty
				title: 'Статус заявки'
				value: {=recGetStr pohod 'claimState'};
			EntityProperty
				title: 'Комментарий руководителя'
				value: {=recGet pohod 'chiefComment'}
				isWide: true;
			EntityProperty
				title: 'Комментарий МКК'
				value: {=recGet pohod 'mkkComment'}
				isWide: true;
			EntityProperty
				title: 'Дополнительные материалы'
				value: {=if extraFileLinks != null {*
				<ol class="{=cls 'extraFileLinksList'}">
					{=repeat linkData in extraFileLinks {*
					<li>
						<a href="{=recGet linkData 'link'}"
							target="__blank"
							title="{=recGet linkData 'name'}"
							>{{ {=recGet linkData 'name'} or {=recGet linkData 'link'} }}</a>
					</li>
					*}}
				</ol>
				*}
					
				}
				isWide: true;
		}
	</div>
	*}
};
}