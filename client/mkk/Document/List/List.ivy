expr {=
from fir.controls.utils import cls, firScopeOptsEnc, hasRight;
from fir.controls.Paging import Paging;
from fir.controls.OpenDialog import OpenDialog;

var
	instanceName: 'documentList'
	cssBaseClass: "mkk-DocumentList"
	RPCMethod: 'document.list'
	requestURI: "/api/document/list";
def DocumentList {=
	def.kv
		moduleName: 'mkk/Document/List/List'
		instanceName: instanceName
		cssBaseClass: cssBaseClass
		RPCMethod: RPCMethod
		requestURI: requestURI
		documentList: null
		nav: null;
	def.body {*
	<div class="{=cls 'block'}" data-fir-module="{{moduleName}}">
		<input type="hidden" data-fir-opts="{=firScopeOptsEnc}"/>
		<h2>Статьи и документы</h2>
		<div class="row">
			<div class="col-auto">
				{=Paging
					instanceName: instanceName ~ 'Paging'
					formField: 'offset'
					nav: nav
				}
			</div>
			{=if {=hasRight obj: 'document.item' kind: 'edit'} {*
			<div class="col text-right">
				<a href="javascript:void(0)"
					class="{=cls 'addDocBtn'} btn btn-primary"
					>Добавить документ</a>
			</div>
			*}}
		</div>

		{=DocumentLinkList
			instanceName: instanceName
			cssBaseClass: cssBaseClass
			RPCMethod: RPCMethod
			documentList: documentList
			nav: nav
		}
		{=OpenDialog
			instanceName: 'documentEditDlg'
			config: {
				ivyModule: 'mkk.Document.Edit',
				ivyMethod: 'DocumentEdit',
				queryParams: {
					num: null
				}
			}
		}
	</div>
	*};
};

def DocumentLinkList {=
	def.kv
		instanceName: instanceName
		cssBaseClass: cssBaseClass
		RPCMethod: RPCMethod
		documentList: null
		nav: null;
	def.body {*
	<div class="{=cls 'linkList'}" data-ivy-method="{{_ivyMethod}}">
	{=
		var i: 0;
		repeat it in documentList {*
		{=set i: i + 1}
		<div class="{=cls 'listItem'} row">
			<div class="col">{{i}}.
				<a class="{=cls 'listLink'}" href="{{it.link}}">{{it.name}}</a>
			</div>
			<div class="col-md-auto">
				{=if {=hasRight obj: 'document.item' kind: 'edit'} {*
				<div class="{=cls 'linkEditBtn'} col g-icon-small g-icon-editItem btn btn-secondary"
					title="Изменить"
					data-document-num="{{it.num}}"></div>
				*}}
			</div>
		</div>
		*}
	}
	</div>
	*};
};
}